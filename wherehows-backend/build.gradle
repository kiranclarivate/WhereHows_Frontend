apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply from: '../gradle/scripts/play.gradle'

// Change this to listen on a different port
project.ext.httpPort = 19001
project.ext.playBinaryBaseName = "wherehows-backend"

dependencies {
    // User defined libraries (will be copied to lib/ before `play compile`)
    play project(":wherehows-common")
    play project(":wherehows-etl")
    play project(":wherehows-dao")

    play externalDependency.play_java_jdbc
    play externalDependency.slf4j_api
    play externalDependency.jasypt

    playTest 'com.github.stefanbirkner:system-rules:1.16.0'
    playTest 'org.easytesting:fest-assert-core:2.0M10'
    playTest externalDependency.mockito
}

model {
    distributions {
        playBinary {
            contents {
                from("jobs") {
                    into "jobs"
                }
                from("../wherehows-data-model/ELASTICSEARCH") {
                    into "ELASTICSEARCH"
                }
                from("script") {
                    into "bin"
                }
                from("${projectDir}/../wherehows-etl/src/main/resources/jython"){
                    into "jython"
                }
            }
        }
    }
}


def nexusUsername="$System.env.ORG_GRADLE_PROJECT_nexusUsername"
def nexusPassword="$System.env.ORG_GRADLE_PROJECT_nexusPassword"

publishing {
    publications {
        pdZipPublication(MavenPublication) {
            groupId 'com.clarivate.wherehows'
            artifactId "${playBinaryBaseName}"
            version 'SNAPSHOT'
            artifact "${projectDir}/build/distributions/${playBinaryBaseName}.zip"
        }
    }
    repositories {
        maven {

            credentials {
                username nexusUsername
                password nexusPassword
            }
            // url 'http://localhost:8081/nexus/content/repositories/releases/'
            url 'http://repo.oneplatform.build/nexus/content/repositories/snapshot/'
        }
    }
}
